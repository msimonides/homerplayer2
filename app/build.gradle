plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    alias(libs.plugins.ksp)
    alias(libs.plugins.protobuf)
}

android {
    namespace 'com.studio4plus.homerplayer2'
    compileSdk 33

    defaultConfig {
        applicationId "com.studio4plus.homerplayer2"
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation":"$projectDir/schemas".toString(),
                        "room.incremental":"true",
                        "room.expandProjection":"true"
                ]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.4'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    sourceSets {
        main {
            java {
                // Add generated protobuf code for koin-annotate to see them:
                srcDirs += "${protobuf.generatedFilesBaseDir}"
            }
        }
    }
    applicationVariants.configureEach { variant ->
        kotlin.sourceSets {
            getByName(name) { kotlin.srcDir("build/generated/ksp/${variant.name}/kotlin") }
        }
    }
}

protobuf {
    protoc {
        artifact = libs.protobuf.protoc.get().toString()
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option("lite")
                }
                kotlin {
                    option("lite")
                }
            }
        }
    }
}

dependencies {

    ksp libs.androidx.room.compiler
    ksp libs.koin.compiler

    implementation libs.accompanist.pager
    implementation libs.androidx.activity.compose
    implementation libs.androidx.core.ktx
    implementation libs.androidx.datastore.core
    implementation libs.androidx.documentfile
    implementation libs.androidx.lifecycle.runtime.compose
    implementation libs.androidx.lifecycle.viewmodel
    implementation libs.androidx.media3.exoplayer
    implementation libs.androidx.media3.session
    implementation libs.androidx.navigation.compose
    implementation libs.androidx.room.ktx
    implementation libs.compose.ui.ui
    implementation libs.compose.ui.tooling.preview
    implementation libs.compose.material.icons.extended
    implementation libs.compose.material3
    implementation libs.koin.android
    implementation libs.koin.androidx.compose
    implementation libs.koin.annotations
    implementation libs.kotlinx.coroutines.android
    implementation libs.protobuf.kotlin.lite
    implementation libs.timber

    testImplementation libs.junit
    androidTestImplementation libs.androidx.test.espresso.core
    androidTestImplementation libs.androidx.test.junit
    androidTestImplementation libs.compose.ui.test.junit

    debugImplementation libs.compose.ui.tooling
    debugImplementation libs.compose.ui.test.manifest
}